type t
(** The type of the inertiajs driver. *)

type page
(** The type of a page, see {{:https://inertiajs.com/the-protocol#the-page-object} The page object}. *)

type prop
(** The type of the properties used to populate the page.

    There are four types of properties the differ in how they are evaluated and interact with partial reload,
    see {{:https://inertiajs.com/partial-reloads#lazy-data-evaluation} Lazy data evaluation}.
    - Regular properties: {b Always} included on standard visits, {b optionally} included on partial reloads
    - Delayed properties: {b Always} included on standard visits, {b optionally} included on partial reloads,
    evaluated when {b needed}
    - Lazy properties: {b Never} included on standard visits, {b optionally} included on partial reloads,
    evaluated when {b needed}
    - Always properties: {b Always} included on standard visits, {b always} included on partial reloads
    *)

type json = Yojson.Safe.t
(** The JSON type. *)

val prop : json -> prop
(** Create a regular property. *)

val lazy_prop : (unit -> json) -> prop
(** Create a lazy property. *)

val delayed_prop : (unit -> json) -> prop
(** Create a delayed property. *)

val always_prop : json -> prop
(** Create an always property. *)

val init : version:string -> root_view:(string -> string) -> unit -> t
(** [init version root_view ()] initialize a driver with the assets version set to [version],
    on standard visits, the root view HTML will be generated by passing the string representation of a page data
    to the [root_view] function *)

val middleware : t -> Dream.middleware
(** Manage assets versioning, csfr token, and non post redirection status. *)

val page :
  component:string -> ?props:(string * prop) list -> url:string -> unit -> page
(** [page component props url ()] create a new page data. *)

val with_prop : page -> string -> prop -> page
(** [with_prop page key prop] create a copy of the page with this property added,
    if the key already exists it will be replaced. *)

val with_url : page -> string -> page
(** [with_url page url ] create a copy of the page with a new URL. *)

val add_shared_data : Dream.request -> string -> json -> unit Lwt.t

val inertia_response :
  t ->
  ?headers:(string * string) list ->
  ?status:Dream.status ->
  Dream.request ->
  page ->
  Dream.response Lwt.t
(** Create a new Dream response by rendering the page data.
    The page will be rendered either as text/html for standard visit, or application/json for inertia request *)

(** Helper to generate a root view. *)
module Root_View_Helper : sig
  val create : js:string -> css:string -> string -> string
  (** [create js css] create a root view for standard visit.
    The template is basic but should be sufficient for most usage.

    [js] the path to the JavaScript asssets.

    [css] the path to the CSS assets*)
end
